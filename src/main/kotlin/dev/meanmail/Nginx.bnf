{
  parserClass="dev.meanmail.psi.parser.NginxParser"
  parserUtilClass="com.intellij.lang.parser.GeneratedParserUtilBase"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix=""
  psiImplClassSuffix="Impl"
  psiPackage="dev.meanmail.psi"
  psiImplPackage="dev.meanmail.psi.impl"

  elementTypeHolderClass="dev.meanmail.psi.Types"
  elementTypeClass="dev.meanmail.psi.NginxElementType"
  tokenTypeClass="dev.meanmail.psi.NginxTokenType"

  psiImplUtilClass="dev.meanmail.psi.PsiImplUtil"
}

nginxFile ::= statements*

private statements ::= (wsps stmts wsps COMMENT? (EOL | <<eof>>)) | EOL
private wsps  ::= WHITE_SPACE*
private stmts ::= (
                   directive_stmt
                   )
name_stmt      ::= IDENTIFIER
value_stmt     ::= (
                   IDENTIFIER
                   | VALUE
                   )
block_stmt     ::= LBRACE statements* RBRACE {pin=1}
values_stmt    ::= value_stmt (WHITE_SPACE+ value_stmt)*
directive_stmt ::= name_stmt wsps values_stmt? wsps (SEMICOLON | block_stmt) {pin=1}
